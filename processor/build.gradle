plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.10'
}

allprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'

    repositories {
        mavenCentral()
    }

    group = 'io.github.shreyashsaitwal.rush'
    version = '2.0.0-beta05'

    compileKotlin {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8
        }
    }
}

subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    java {
        withSourcesJar()
        withJavadocJar()
    }

    publishing {
        repositories {
            maven {
                name = 'OSSRH'
                url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                credentials {
                    username = project.properties['ossrh.username']
                    password = project.properties['ossrh.password']
                }
            }
        }

        publications {
            mavenJava(MavenPublication) {
                pom {
                    url = 'https://github.com/shreyashsaitwal/rush-cli/tree/2.0/processor'
                    licenses {
                        license {
                            name = 'GPL-3.0'
                            url = 'https://raw.githubusercontent.com/shreyashsaitwal/rush-cli/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'shreyashsaitwal'
                            name = 'Shreyash Saitwal'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/shreyashsaitwal/rush-cli.git'
                        developerConnection = 'scm:git:ssh://github.com/shreyashsaitwal/rush-cli.git'
                        url = 'https://github.com/shreyashsaitwal/rush-cli'
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }
}
